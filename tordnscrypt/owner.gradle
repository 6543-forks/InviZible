apply plugin: 'com.android.application'

def keystorePropertiesFile = file("C:\\KStore\\keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 29


    flavorDimensions "version", "processor"

    signingConfigs {
        stablesign {
            storeFile file("C:\\KStore\\pan_alexander.jks")
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keySAlias']
            keyPassword keystoreProperties['keySPassword']
        }

        betasign {
            storeFile file("C:\\KStore\\pan_alexander.jks")
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyBAlias']
            keyPassword keystoreProperties['keyBPassword']
        }
    }

    productFlavors {
        lite {
            applicationId "pan.alexander.tordnscrypt.stable"
            versionName "3.0.1"
            dimension = 'version'
            signingConfig signingConfigs.stablesign
            resValue 'string', 'package_name', applicationId
        }

        pro {
            applicationId "pan.alexander.tordnscrypt.stable"
            versionName "3.0.1"
            dimension = 'version'
            signingConfig signingConfigs.stablesign
            resValue 'string', 'package_name', applicationId
        }

        beta {
            applicationId "pan.alexander.tordnscrypt"
            versionName "0.5.8"
            dimension = 'version'
            signingConfig signingConfigs.betasign
            resValue 'string', 'package_name', applicationId
        }

        armv7a {
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'armv7a'
            versionCode = 2

            ndk {
                abiFilters 'armeabi-v7a'
            }
        }

        arm64 {
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'arm64'
            versionCode = 3

            ndk {
                abiFilters 'arm64-v8a'
            }
        }

    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 58

        resConfigs "en", "ru-rRU", "ru-rUA", "pl"

        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-19"
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    buildToolsVersion = '29.0.2'

    bundle {
        language {
            enableSplit = false
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs 'libs/'
        }
    }

    packagingOptions{
        doNotStrip '*/*/libdnscrypt-proxy.so'
        doNotStrip '*/*/libi2pd.so'
        doNotStrip '*/*/libobfs4proxy.so'
        doNotStrip '*/*/libtor.so'
    }

    applicationVariants.all { variant ->

        def abiVersion = variant.mergedFlavor.versionCode

        variant.outputs.each { output ->
            output.versionCodeOverride = abiVersion * 1000 + defaultConfig.versionCode
        }
    }

}



repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.2.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    //noinspection GradleDynamicVersion
    implementation 'eu.chainfire:libsuperuser:1.1.0.+'
    implementation 'com.github.angads25:filepicker:1.1.1'
    implementation 'com.jrummyapps:android-shell:1.0.1'
}
